set(THREADX_ARCH cortex_m7)
set(THREADX_TOOLCHAIN gnu)
set(TX_USER_FILE ${PROJECT_SOURCE_DIR}/platform/runtime/tx_user.h)
add_subdirectory(${PROJECT_SOURCE_DIR}/external/threadx)
target_link_libraries(threadx PRIVATE project_compiler_settings)

set(FX_USER_FILE ${PROJECT_SOURCE_DIR}/platform/runtime/fx_user.h)
add_subdirectory(${PROJECT_SOURCE_DIR}/external/filex)
target_link_libraries(filex PRIVATE project_compiler_settings)

# This setting changes ThreadX-visible structures and must be consistent for
# the kernel and every consumer of tx_api.h.
target_compile_definitions(threadx PUBLIC TX_ENABLE_STACK_CHECKING)

add_library(platform OBJECT)

# Startup and ThreadX low-level objects must be present directly in the final
# link. A static archive is insufficient because their references appear only
# after GNU ld reaches the ThreadX archive.
target_sources(platform
    PRIVATE
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/main.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/eth.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/gpio.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/rng.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/rtc.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/tim.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/stm32h7xx_it.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/stm32h7xx_hal_msp.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/stm32h7xx_hal_timebase_tim.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/sysmem.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/system_stm32h7xx.c
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/tx_initialize_low_level.S
        ${PROJECT_SOURCE_DIR}/external/CubeMX/startup_stm32h753xx.s
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_tim.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_tim_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_cortex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_eth.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_eth_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_fdcan.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rcc.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rcc_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_flash.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_flash_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_gpio.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_hsem.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_dma.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_dma_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_mdma.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_pwr.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_pwr_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_i2c.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_i2c_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_exti.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rng.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rng_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rtc.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_rtc_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_usart.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_usart_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_uart.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Src/stm32h7xx_hal_uart_ex.c
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-nucleo-bsp/stm32h7xx_nucleo.c  
)

# CubeMX owns these functions outside USER CODE sections. Rename only the
# generated definitions so project-owned implementations remain stable across
# regeneration. Calls inside main.c still reach CubeMX_Error_Handler(), whose
# preserved USER CODE body delegates to hal_error_handler().
set_source_files_properties(
    ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/main.c
    PROPERTIES
        COMPILE_DEFINITIONS "Error_Handler=CubeMX_Error_Handler"
)
set_source_files_properties(
    ${PROJECT_SOURCE_DIR}/external/CubeMX/Src/stm32h7xx_it.c
    PROPERTIES
        COMPILE_DEFINITIONS "EXTI15_10_IRQHandler=CubeMX_EXTI15_10_IRQHandler"
)

include(${PROJECT_SOURCE_DIR}/cmake/verify_cubemx.cmake)
verify_cubemx_generation()

target_include_directories(platform
    PRIVATE
        ${PROJECT_SOURCE_DIR}/platform
    PUBLIC
        # tx_api.h includes this as "tx_user.h". It must precede CubeMX/Inc,
        # which also contains CubeMX's generated tx_user.h.
        ${PROJECT_SOURCE_DIR}/platform/runtime
        ${PROJECT_SOURCE_DIR}/external/CubeMX/Inc
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Inc
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-hal-driver/Inc/Legacy
        ${PROJECT_SOURCE_DIR}/external/stm32h7xx-nucleo-bsp
        ${PROJECT_SOURCE_DIR}/external/cmsis-core/CMSIS/Core/Include
        ${PROJECT_SOURCE_DIR}/external/cmsis-device-h7/Include
)

target_compile_definitions(platform
    PRIVATE
        # Ethernet descriptors and raw frame buffers are linked into D2
        # SRAM2/SRAM3. SystemInit() uses this definition to enable all D2 SRAM
        # interfaces before the MPU, D-cache, and Ethernet DMA are started.
        DATA_IN_D2_SRAM
    PUBLIC
        USE_PWR_LDO_SUPPLY
        USE_HAL_DRIVER
        STM32H753xx
        $<$<CONFIG:Debug>:DEBUG>
)

target_link_libraries(platform
    PUBLIC
        filex
        threadx
)

target_compile_features(platform
    PUBLIC
        c_std_11
)
